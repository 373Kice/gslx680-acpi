#!/usr/bin/perl

use strict;
use warnings;
use IO::File;

sub usage() {
	print STDERR "Usage: tscfg2fw GSL_TS_CFG.h gsl1680.fw\n";
	-1;
}

my $headerfile = $ARGV[0] or exit usage;
my $fwfile = $ARGV[1] or exit usage;

my $in = IO::File->new($headerfile, 'r') or die "Can't open $headerfile: $!";
my $out = IO::File->new($fwfile, 'w') or die "Can't open $fwfile: $!";
$out->binmode;

my $cfg = 0;
while (my $line = <$in>) {
	if ($cfg and $line =~ /};/) {
		$cfg = 0;
	}
	if ($line =~ /TS_CFG_DATA/) {
		$cfg = 1;
	}
	if ($cfg) {
		$line =~ s/\s//g;
		$line = lc($line);
		if ($line =~ /{0x([0-9a-f]+),0x([0-9a-f]+)},/) {
			my $address = hex($1);
			my $data = hex($2);
			print $out pack('(LL)<', $address, $data);
		}
	}
}

$out->close;
$in->close;